apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'realm-android'
apply plugin: 'com.google.firebase.firebase-crash'

android {
    ext {
        version_number = "1.3.4"
    }
    compileSdkVersion compile_sdk_version
    buildToolsVersion build_tools_version
    defaultConfig {
        multiDexEnabled true
        applicationId "com.assetic.mobility"
        minSdkVersion min_sdk_version
        targetSdkVersion target_sdk_version
        versionCode 39
        versionName "Release $version_number ($versionCode)"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        resValue "string", "app_name", "Assetic "
        resValue "string", "app_version_friendly", "Android $version_number"
    }
flavorDimensions "release"
    productFlavors {
        external {
            dimension "release"
        }
        internal {
            dimension "release"
            resValue "string", "app_name", "Assetic - Preview"
            resValue "string", "app_version_friendly", "Android $version_number - Preview"
            versionName "Preview 5"
        }
        dev {
            dimension "release"
            versionNameSuffix "-dev"
            applicationIdSuffix '.dev'
            resConfigs "en", "xxxhdpi"
            resValue "string", "app_name", "Assetic - Developer"
            resValue "string", "app_version_friendly", "Android $version_number - Developer"
            versionName "Developer 1"
        }
    }
    buildTypes {
        debug {
            resValue "string", "api_key", "AIzaSyCmYfbpWw625ct3R7zTYu0jhkdyuYWYFS4"
        }
        release {
            resValue "string", "api_key", "AIzaSyD2or9u2olyov0YNTRIdcEzDk3ciPspJd0"
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }
    dexOptions {
        preDexLibraries true
        javaMaxHeapSize "2g"
    }
    packagingOptions {
        pickFirst "META-INF/rxjava.properties"
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')

    /* Android view compilation */
    implementation google_support_dependencies.values()

    /* External view compilation */
    implementation('com.mikepenz:materialdrawer:5.8.1@aar') {
        transitive = true
    }

    implementation google_services_dependencies.values()
    implementation retrofit_dependencies.values()

    implementation dagger_dependencies.dagger
    kapt dagger_dependencies.compiler

    implementation kotlin_dependencies.values()

    implementation rx_java_dependencies.values()
    implementation view_dependencies.values()
    implementation util_dependences.values()
    implementation util_dependences.values()
    implementation firebase_dependences.values()

    implementation architecture_depenencies.extensions
    implementation architecture_depenencies.view_model
    kapt architecture_depenencies.comp

    implementation 'me.dm7.barcodescanner:zxing:1.9.8'
    implementation 'com.github.chrisbanes:PhotoView:2.1.3'

    implementation project(':models')
    implementation project(':api')
    implementation project(':dal')
}
repositories {
    mavenCentral()
}

apply plugin: 'com.google.gms.google-services'
